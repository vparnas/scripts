#!/usr/bin/perl

use strict;
use warnings;

( $ARGV[0] ) or die "Must specify input file and optional base variable\n";

my ($infile, $basevar) = @ARGV;
my ($input, $output);

open(INFILE, "< $infile") or die "Couldn't open $infile: $!\n";
{
    local $/; # enable "slurp" mode
    $input = <INFILE> . "\n";
}
close(INFILE);

if ($basevar) {
    $output = "\@$basevar\@";
} elsif ($input =~ /\#(\@\w+\@)/) { # extract variable from first block
    $output = $1;
} else {
     die "No variable block defined in $infile\n";
}
while ($output =~ /\@\w+\@/) {
    my $varname = $&;
    ($input =~ /\#$varname\n(.*?)\n\n/s)
        or die "Did not find variable $varname in $infile.\n";
    my @varblock = split (/\n/, $1);
    my $randline = $varblock[rand @varblock];
    $randline =~ s/(.*)\s+\#.*/$1/; # Strip trailing (pound-sign) comments
    $output =~ s/$varname/$randline/;
}
print "$output\n";
